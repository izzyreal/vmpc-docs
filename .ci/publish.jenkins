#!groovyâ€‹

// TODO: Move to a file and avoid modifying CI script
Map<String, String> versions = [
        'master': '0.4'
        ]

node('Linux') {

    String prefix = params.prefix ?: "${BUILD_URL}artifact/gh-pages/"
    stage('Check params') {
        assert params.latest, 'Provide a name to the "latest" branch'
        if (params.publish) {
            assert params.prefix, 'Provide the prefix (base URL) if you are going to publish'
        }

        echo """
            Generate VMPC2000XL docs webpage
             - params.latest: ${params.latest}
             - params.publish: ${params.publish}
             - prefix: ${prefix}
             - versions: latest (${params.latest}), ${versions.collect({key, value -> value }).join(', ')}
        """
        versions[params.latest] = 'latest'
    }

    def image = null
    stage('Build docker image') {
        // Build the docker image using the same commit as the current 'publish.jenkins' file
        dir('tmp') {
            checkout scm
            image = docker.build('vmpc-docs', '-f .ci/Dockerfile .')  // It should cache the image
            deleteDir()
        }
    }

    stage('Prepare sources as worktrees') { // Do this sequentially
        sh 'git clone --bare https://github.com/izzyreal/vmpc-docs.git src'
        dir('src') {
            versions.each { branchName, folderName ->
                sh "git fetch origin ${branchName}:${branchName}"
                sh "git worktree add ../${folderName} ${branchName}"
            }
            // Prepare the worktree for gh-pages too
            sh 'git fetch origin gh-pages:gh-pages'  // TODO: This is a very bad idea, we are retrieving something (100s Mb) we are going to remove
            sh 'git worktree add ../gh-pages gh-pages'
        }
        sh 'rm -fr gh-pages/.git'
        sh 'rm -fr gh-pages/en'
    }

    Map parallelJobs = [:]
    versions.each { key, value ->
        String branchName = key
        String folderName = value
        parallelJobs[folderName] = {
            echo "Run parallel job for ${branchName} inside ${folderName}"
            image.inside {
                stage('Prepare sources') {
                    writeJSON json: versions, file: "${folderName}/versions.json"
                    if (folderName != 'latest') {
                        sh "rm -fr ${folderName}/_themes/vmpc2000xl"
                        sh "cp -a latest/_themes/. ${folderName}/_themes/"
                    }
                }

                stage('HTML') {
                    sh "sphinx-build -W -b html -d ${folderName}/_build/.doctrees ${folderName}/ gh-pages/en/${folderName}"
                }
            }
        }
    }

    stage('Generate all releases') {
        parallelJobs.failFast = true
        parallel parallelJobs
    }

    stage('Prepare gh-branch') {
        dir('gh-pages') {
            // sh 'cp src/versions.json versions.json'  // TODO: File is not used, remove from 'gh-pages' branch
            sh 'cp en/latest/404.html 404.html'

            String content = readFile('404.html')
            String prefixLatest = "${prefix}en/latest"
            content = content.replaceAll('href="_', "href=\"${prefixLatest}/_")
            content = content.replaceAll('src="_', "src=\"${prefixLatest}/_")
            content = content.replaceAll('alt="_', "alt=\"${prefixLatest}/_")
            content = content.replaceAll('internal" href="', "internal\" href=\"${prefixLatest}/")
            content = content.replaceAll('"search.html"', "\"${prefixLatest}/search.html\"")
            content = content.replaceAll('"genindex.html"', "\"${prefixLatest}/genindex.html\"")
            writeFile(file: '404.html', text: content)
        }
    }

    stage('Archive generated folder') {
        archiveArtifacts artifacts: 'gh-pages/**/*.*'
        echo "Inspect generated webpage at ${BUILD_URL}artifact/gh-pages/index.html"
    }

    if (params.publish) {
        stage('Publish to gh-pages') {
            dir('gh-pages') {
                sh 'git init .'
                sh 'git checkout -b gh-pages'
                sh 'git add .'
                sh 'git config user.email "izmaelverhage@gmail.com"'
                sh 'git config user.name "IzmarCI bot"'
                sh "git commit -m \"Automatic deploy (build number ${BUILD_NUMBER})\""
                withCredentials([usernamePassword(credentialsId: 'izmarci-gh-token', usernameVariable: 'GH_USER', passwordVariable: 'GH_PASS')]) {
                    sh "git remote add origin-pages https://$GH_USER:$GH_PASS@github.com/izzyreal/vmpc-docs.git"
                    sh 'git push --quiet --force origin-pages gh-pages'
                }
            }
        }
    }
}
